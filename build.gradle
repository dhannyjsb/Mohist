buildscript {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenLocal()
        mavenCentral()
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url 'https://raw.githubusercontent.com/PFCraft/MohistCentral/master/repository' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'net.nemerosa:versioning:2.7.1'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "idea"
apply plugin: 'net.minecraftforge.gradle.patcher'
apply plugin: 'net.minecraftforge.gradle.launch4j'
apply plugin: 'net.nemerosa.versioning'

group = 'red.mohist'
version = '1.1-'+versioning {
    full {it.abbreviated}
    dirty = { t -> t }
}.info.full

repositories {
    clear()
    mavenCentral()
    maven { url 'https://files.minecraftforge.net/maven' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url 'https://raw.githubusercontent.com/PFCraft/MohistCentral/master/repository' }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

configurations.all {
    transitive = false
}

dependencies {
    compile 'org.avaje:ebean:2.8.1'
    compile 'com.alibaba:fastjson:1.2.58'

    // 以下库仅仅是为了让开发人员更好地开发
    // 以下庫僅僅是為了讓開發人員更好地開發
    // The following libraries are just for better development by developers
    // 以下のライブラリは開発者がより良い開発をするためだけです。
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile 'com.mojang:patchy:1.1'
    compile 'oshi-project:oshi-core:1.1'
    compile 'net.java.dev.jna:jna:4.4.0'
    compile 'net.java.dev.jna:platform:3.4.0'
    compile 'com.ibm.icu:icu4j-core-mojang:51.2'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.3'
    compile 'com.paulscode:codecjorbis:20101023'
    compile 'com.paulscode:codecwav:20101023'
    compile 'com.paulscode:libraryjavasound:20101123'
    compile 'com.paulscode:librarylwjglopenal:20100824'
    compile 'com.paulscode:soundsystem:20120107'
    compile 'io.netty:netty-all:4.1.9.Final'
    compile 'com.google.guava:guava:21.0'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-io:commons-io:2.5'
    compile 'commons-codec:commons-codec:1.10'
    compile 'net.java.jinput:jinput:2.0.5'
    compile 'net.java.jutils:jutils:1.0.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.mojang:authlib:1.5.25'
    compile 'com.mojang:realms:1.10.21'
    compile 'org.apache.commons:commons-compress:1.8.1'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'commons-logging:commons-logging:1.1.3'
    compile 'org.apache.httpcomponents:httpcore:4.3.2'
    compile 'it.unimi.dsi:fastutil:7.1.0'
    compile 'org.apache.logging.log4j:log4j-api:2.8.1'
    annotationProcessor 'org.apache.logging.log4j:log4j-core:2.8.1' // log4j uses annotation processors
    compile 'org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209'
    compile 'org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209'
    compile 'com.mojang:text2speech:1.10.3'
    compile 'net.minecraft:launchwrapper:1.12'
    compile 'jline:jline:2.13'
    compile 'org.ow2.asm:asm-debug-all:5.2'
    compile 'com.typesafe.akka:akka-actor_2.11:2.3.3'
    compile 'com.typesafe:config:1.2.1'
    compile 'lzma:lzma:0.0.1'
    compile 'java3d:vecmath:1.5.2'
    compile 'net.sf.trove4j:trove4j:3.0.3'
    compile 'org.apache.maven:maven-artifact:3.5.3'
    compile 'org.yaml:snakeyaml:1.21'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    compile 'commons-lang:commons-lang:2.6' // Is needed for plugin compatibility
    compile 'net.md-5:SpecialSource:1.7.4'
    compile 'org.scala-lang.modules:scala-parser-combinators_2.11:1.0.1'
    compile 'org.xerial:sqlite-jdbc:3.21.0.1'
    compile 'mysql:mysql-connector-java:5.1.45'
    compile 'net.md-5:bungeecord-chat:1.12-SNAPSHOT'    
    // compile 'com.maxqia:ReflectionRemapper:0.3-SNAPSHOT'
}

minecraft.version = "1.12.2"

minecraft {
    mappings = 'snapshot_nodoc_20171003'
    workspaceDir = "projects"
    versionJson = "jsons/${minecraft.version}-mohist-dev.json"
    buildUserdev = false
    buildInstaller = true
    installerVersion = "1.5"

    def common = {
        patchPrefixOriginal "../src-base/minecraft"
        patchPrefixChanged "../src-work/minecraft"
        mainClassClient "net.minecraft.launchwrapper.Launch"
        tweakClassClient "net.minecraftforge.fml.common.launcher.FMLTweaker"
        mainClassServer "net.minecraft.launchwrapper.Launch"
        tweakClassServer "net.minecraftforge.fml.common.launcher.FMLServerTweaker"
    }

    projects {
        forge {
            rootDir "forge/"
            patchDir "forge/patches/minecraft/"
            patchAfter "clean"
            genPatchesFrom "clean"
            genMcpPatches = false
            applyMcpPatches = false
            s2sKeepImports = true
            with common
        }

        mohist {
            rootDir "."
            patchDir "patches/"
            patchAfter "forge"
            genPatchesFrom "forge"
            genMcpPatches = true
            applyMcpPatches = true
            s2sKeepImports = true
            with common
        }
    }
}

sourceCompatibility = 1.8
tasks.generateXmlConfig.dependsOn installer
tasks.build.dependsOn 'launch4j'

installer {
    classifier = 'installer'
    from "src/main/resources/mohist_logo.png"
    rename "mohist_logo\\.png", "big_logo.png"
}

launch4j {
    jar = installer.archivePath.canonicalPath
    outfile = file("build/distributions/${project.name}-${project.version}-installer-win.exe").canonicalPath
    icon = file('icon.ico').canonicalPath
    manifest = file('l4jManifest.xml').canonicalPath
    jreMinVersion = '1.8.0'
    initialHeapPercent = 5
    maxHeapPercent = 100
}

task signUniversal(type: SignJar, dependsOn: 'outputJar') {
    onlyIf {
        project.hasProperty('jarsigner')
    }

    def jarsigner = [:]

    if (project.hasProperty('jarsigner'))
        jarsigner = project.jarsigner

    alias = 'forge'
    exclude "paulscode/**"
    storePass = jarsigner.storepass
    keyPass = jarsigner.keypass
    keyStore = jarsigner.keystore
    inputFile = outputJar.archivePath
    outputFile = outputJar.archivePath
}
uploadArchives.dependsOn signUniversal
build.dependsOn signUniversal
installer.dependsOn signUniversal

outputJar {
    classifier = 'universal'

    manifest.attributes([
            "Implementation-Title": "Mohist",
            "Implementation-Version": version,
            "Main-Class": "red.mohist.Mohist",
            "TweakClass": "net.minecraftforge.fml.common.launcher.FMLTweaker",
            "Class-Path": getServerClasspath(file("jsons/${minecraft.version}-mohist-rel.json"))
    ])
}

processJson {
    releaseJson = "jsons/${minecraft.version}-mohist-rel.json"
    addReplacements([
            "@minecraft_version@": project.minecraft.version,
            "@version@": project.version,
            "@project@": "mohist",
            "@artifact@": "red.mohist:mohist:${project.version}",
            "@universal_jar@": { outputJar.archiveName },
            "@timestamp@": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
    ])
}

extractForgeSources {
    exclude "**/SideOnly.java", "**/Side.java"
}

genGradleProjects {
    addCompileDep "junit:junit:4.12"
    addCompileDep "org.junit.jupiter:junit-jupiter-api:5.0.0"
    addCompileDep "org.opentest4j:opentest4j:1.0.0" // needed for junit 5
    addCompileDep "org.hamcrest:hamcrest-core:1.3"
}

import groovy.json.JsonSlurper

String getServerClasspath(File file) {
    def node = new JsonSlurper().parse(file)
    def out = new StringBuilder()
    node.versionInfo.libraries.each { lib ->
        if (lib.serverreq)
        {
            def split = lib.name.split(':')
            def group = split[0].replace('.', '/')
            def artifact = split[1]
            def version = split[2]
            out += "libraries/$group/$artifact/$version/$artifact-${version}.jar "
        }
    }
    out += "minecraft_server.${minecraft.version}.jar"
    return out.toString()
}

task dumpLibs(type: Copy) {
    into "$buildDir/libs/libraries"
    from configurations.compile
}